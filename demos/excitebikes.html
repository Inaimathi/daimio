<!doctype html>
<html lang="en" data-framework="javascript">
<head>
  <title>Excitable Media</title>
  <script type="text/javascript" src="../node_modules/underscore/underscore.js"></script>
  <script type="text/javascript" src="../get.php?file=node_modules/daml"></script>
</head>
<body>
  
  <div id="house"></div>
  
  
  <script type="text/daml" class="spaceseeds">

    outer
      $colors ["#339", "#66B", "#66F", "#BBF", "#EEF"]
      $time 30
      $cells []
      
      // INIT
      @init  from-js
      changed {__}
      show {__}
      initializer
        {20 | range | map block "{20 | range | map block 0}" | $>cells}
      
      @init -> initializer -> process -> changed


      // LOOP
      process 
        { $cells | map block "{
            __ | map with {* (:row _key)} block "{
              __ | switch (
                0 { $cells.{_row | add -1 | range 3}.*.{_key | add -1 | range 3}.*
                  | math max | >max | _max | less than 3 | then 0 else _max}
                1 0 
                2 1
                3 2
                4 3)}"}"
        | $>cells}

      process -> {process sleep for $time} -> process
      
      
      // INPUT
      @touched dom-on-click .touch
      activate
        {__ | split on "x" | $>coords | $cells | map block "{__ | map with {* (:row _key)} block "{_row | eq $coords.#1 | and {_key | eq $coords.#2} | then 4 else __in}"}" | $>cells}

// {__ | split on "x" | >coords | 4 | $>cells.{_coords.#1}.{_coords.#2}}


      @touched -> activate -> changed


      // SHOW
      @house  dom-set-html
      
      show-cells
        <table border="0" cellspacing="0" cellpadding="0">
          {begin row | map data $cells | join}
            <tr>
              {begin item | map data __in with {* (:row _key)} | join}
                <td class="touch" data-value="{_row}x{_key}" style="width:20px;height:20px;background-color:{$colors.{__}}"></td>
              {end item}
          	</tr>
        	{end row}
        </table>
      
      changed -> show
      show -> show-cells -> @house
      
  </script>
   
  
  <script type="text/javascript">    
    // {64 | math random | math pow exp 0.1666 | math round}
    
    document.addEventListener('DOMContentLoaded', function() {
      var seedlikes = [].slice.call(document.getElementsByClassName('spaceseeds'))
                              .map(function(node) {return node.text})
                             .join("\n")
      
      var outerseed = DAML.make_some_space(seedlikes)
      OuterSpace = new DAML.Space(outerseed) // published for debug
    
      // activate init
      DAML.send_value_to_js_port('init', 1)
    })
        
  </script>
  
</body>
</html>