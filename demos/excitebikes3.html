<!doctype html>
<html lang="en" data-framework="javascript">
<head>
  <title>Excitable Media</title>
  <!--
  <script type="text/javascript" src="../daimio_composite.js"></script>
  -->
    <script type="text/javascript" src="../get.php?file=daimio"></script>
</head>
<body>
  
  <div id="house"></div>
  
  
  <script type="text/daimio" class="spaceseeds">

    outer
      $colors ["#66F", "#77F", "#88F", "#99F", "#AAF", "#BBF", "#CCF", "#DDF", "#EEF", "#FFF"]
      $time   1000
      $cells  []
      $mods  []
      
      // INIT
      @init   from-js
      changed {__}
      show    {__}
      initializer
        {20 | range | map block "{20 | range | map block 1}" | >$cells}
        // {64 | math random | math pow exp 0.1666 | math round}
        // {0 | >$cells.{20 | range}.{range 20}}
      
      @init -> initializer


      // LOOP
      process 
        { () | >$mods | 0 | >$max
        | $cells | map block "{
            __ | map with {* (:row _key)} block "{
              __ | max $max | >$max 
                 | __in | mod 2 
                 | switch
                   ( 0 "{ __ | mod 8
                        | then "{ __ | divide by 2}"
                          else "{ $mods | poke (_row _key 1) | >$mods | 0}"
                          with {* (:row _row :key _key "__in" __in)} }"
                     1 "{ __ | times 3 | add 1}")
                   with {* (:row _row :key _key "__in" __in)} 
                 | else 0}"}"
        | >$cells
       || $mods | each block "{ 
            __ | >x | (
              {$cells.{_x.#1 | minus 1 | max 0}.{_x.#2} | add _x.#3 | >$cells.{_x.#1 | minus 1 | max 0}.{_x.#2}}
              {$cells.{_x.#1 | add 1 | min 19}.{_x.#2}  | add _x.#3 | >$cells.{_x.#1 | add 1 | min 19}.{_x.#2}}
              {$cells.{_x.#1}.{_x.#2 | minus 1 | max 0} | add _x.#3 | >$cells.{_x.#1}.{_x.#2 | minus 1 | max 0}}
              {$cells.{_x.#1}.{_x.#2 | add 1 | min 19}  | add _x.#3 | >$cells.{_x.#1}.{_x.#2 | add 1 | min 19}}
            )}" }"}"}


// TODO: add 'with' to switch
// THINK: how do we do the mapping w/o being stupid? maybe a fun over a path into $cells? that could work... so reach in, modify, spit out whole thing -- like a map that only runs over certain items.

// { $cells | poke path (( ({_x.#1} {_x.#2 | add 1}) 
//                         ({_x.#1} {_x.#2 | minus 1}) 
//                         ({_x.#1 | add 1}   {_x.#2}) 
//                         ({_x.#1 | minus 1} {_x.#2}) )) 
//                 block "{__ | _x.#3}" with {* (:x _x)}
// | >$cells

// actually, reduce over $mods with $cells as the initial data -- then you can just >$cells once at the end.


      initializer -> process -> changed
      process -> {process sleep for $time} -> process
      
      
      // INPUT
      @touched dom-on-click .touch
      activate
        { __ | split on "x" | >coords | $cells 
        | list poke path  (_coords.#1 _coords.#2) 
                    value { $cells.{_coords.#1}.{ _coords.#2} 
                          | add 59} 
        | >$cells}
        // {__ | split on "x" | >coords | 4 | >$cells.{_coords.#1}.{_coords.#2}}
      @touched -> activate -> changed
        

      // SHOW
      @house  dom-set-html
      
      show-cells
        <table border="0" cellspacing="0" cellpadding="3" style="max-width:400px;">
          {begin row | map data $cells | join}
            <tr>
              {begin item | map data __in with {* (:row _key)} | join}
                <td class="touch" data-value="{_row}x{_key}" style="max-width:20px;min-width:20px;height:20px;background-color:{$colors.{__ | divide by $max | math pow exp 0.1 | times 10 | math round}};overflow:hidden">{__}</td>
              {end item}
          	</tr>
        	{end row}
        </table>
        <h1>max: {$max}</h1>
      
      changed -> show
      show -> show-cells -> @house
      
  </script>
  

  <script type="text/javascript">    
    document.addEventListener('DOMContentLoaded', function() {
          seedlikes = [].slice.call(document.getElementsByClassName('spaceseeds'))
                              .map(function(node) {return node.text})
                              .join("\n")  // published for editor
      
      var outerseed = D.make_some_space(seedlikes)
      OuterSpace = new D.Space(outerseed) // published for debug
    
      D.send_value_to_js_port('init', 1) // activate init
    })
  </script>
  
</body>
</html>